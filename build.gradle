plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'gg.jte.gradle' version '3.2.1'
    id 'com.diffplug.spotless' version '8.0.0'
    id 'org.graalvm.buildtools.native' version '0.11.2'
    id 'pl.allegro.tech.build.axion-release' version '1.21.0'
}

scmVersion {
    tag {
        prefix.set("")
    }
}

group = 'io.github.gadnex'
version = scmVersion.version
description = 'Sample application to experiment with Datastar'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
//    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'gg.jte:jte-spring-boot-starter-3:3.2.1'
    implementation 'io.github.gadnex:jte-localizer-spring-boot-starter:1.0.3'
    implementation 'io.github.gadnex:jte-datastar-spring-boot-starter:0.2.5'
    jteGenerate 'gg.jte:jte-native-resources:3.2.1'
    implementation 'org.webjars:webjars-locator-lite:1.1.2'
    runtimeOnly("org.webjars.npm:picocss__pico:2.1.1")
    runtimeOnly 'org.webjars.npm:material-icons-font:2.1.0'
    runtimeOnly 'org.webjars.npm:starfederation__datastar:1.0.0-beta.11'
    runtimeOnly 'org.webjars.npm:howler:2.2.4'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jte {
    generate()
    binaryStaticContent = true
    jteExtension("gg.jte.nativeimage.NativeResourcesExtension")
}

jar {
    dependsOn precompileJte
    from fileTree("jte-classes") {
        include "**/*.class"
        include "**/*.bin" // Only required if you use binary templates
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('bootRunDev') {
    group = 'application'
    description = 'Runs the Spring Boot application with the dev profile'
    doFirst {
        tasks.bootRun.configure {
            systemProperty('spring.profiles.active', 'local')
        }
    }
    finalizedBy("bootRun")
}

bootBuildImage {
    imageName = "gadnex/datastar-spring-mvc:${version}"
    environment["BP_JVM_VERSION"] = "25"
    environment["BP_NATIVE_IMAGE_BUILD_ARGUMENTS"] = "-march=compatibility"
}

spotless {
    java {
        googleJavaFormat()
        target 'src/*/java/**/*.java'
    }
}